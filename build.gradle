plugins {
    id 'com.github.ben-manes.versions' version '0.15.0'
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'net.researchgate.release' version '2.6.0'
	id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'org.sonarqube' version '2.5'
}

apply plugin: 'maven'
apply plugin: 'osgi'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'checkstyle'
apply plugin: 'com.github.hierynomus.license'
apply plugin: 'jacoco'
apply plugin: 'project-report'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.trellisldp'
description = 'Trellis HTTP API'

ext {
    vendor = 'Trellis LDP'
    homepage = 'http://www.trellisldp.org'
    docURL = 'http://www.trellisldp.org/apidocs'
    license = 'Apache 2'

    /* Dependencies */
    trellisApiVersion = '0.3.0'
    trellisVocabularyVersion = '0.1.0'
    commonsRdfVersion = '0.3.0-incubating'
    commonsCodecVersion = '1.10'
    commonsIoVersion = '2.6'
    slf4jVersion = '1.7.25'
    jaxrsVersion = '2.1'
    javaxAnnotationsVersion = '1.3.1'
    javaxJsonVersion = '1.1'
    javaxInjectVersion = '1_2'
    metricsVersion = '3.2.5'

    /* Testing */
    junitVersion = '5.0.1'
    junitPlatformVersion = '1.0.1'
    apiguardianVersion = '1.0.0'
    mockitoVersion = '2.11.0'
    trellisJenaIOVersion = '0.2.0'
    trellisAuditVersion = '0.1.0'
    trellisEventVersion = '0.1.0'
    trellisConstraintVersion = '0.2.2'
    jerseyVersion = '2.26'
    logbackVersion = '1.2.3'
    jsonVersion = '1.1'
    jacksonVersion = '2.9.2'
    jacocoVersion = "0.7.9"

    /* OSGi */
    trellisApiVersionRange = '[0.3,1)'
    trellisVocabularyVersionRange = '[0.1,1)'
    spiflyVersion = '1.0.8'
    projectOsgiVersion = project.version.replaceAll("-SNAPSHOT", ".SNAPSHOT")
}


configurations {
    buildToolsConfig
}

dependencies {
    api group: 'javax.annotation', name: 'javax.annotation-api', version: javaxAnnotationsVersion
    api group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: jaxrsVersion
    api group: 'io.dropwizard.metrics', name: 'metrics-annotation', version: metricsVersion
    api group: 'org.apache.commons', name: 'commons-rdf-api', version: commonsRdfVersion
    api group: 'org.trellisldp', name: 'trellis-api', version: trellisApiVersion
    api group: 'org.apache.servicemix.bundles', name: 'org.apache.servicemix.bundles.javax-inject', version: javaxInjectVersion

    implementation group: 'javax.json', name: 'javax.json-api', version: javaxJsonVersion
    implementation group: 'commons-codec', name: 'commons-codec', version: commonsCodecVersion
    implementation group: 'commons-io', name: 'commons-io', version: commonsIoVersion
    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    implementation group: 'org.trellisldp', name: 'trellis-vocabulary', version: trellisVocabularyVersion

    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    testImplementation group: 'org.glassfish', name: 'javax.json', version: jsonVersion
    testImplementation group: 'org.glassfish.jersey.core', name: 'jersey-server', version: jerseyVersion
    testImplementation group: 'org.glassfish.jersey.test-framework.providers', name: 'jersey-test-framework-provider-inmemory', version: jerseyVersion
    testImplementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: jerseyVersion
    testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: apiguardianVersion
    testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: junitPlatformVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    testImplementation group: 'org.trellisldp', name: 'trellis-io-jena', version: trellisJenaIOVersion
    testImplementation group: 'org.trellisldp', name: 'trellis-audit', version: trellisAuditVersion
    testImplementation group: 'org.trellisldp', name: 'trellis-event-serialization', version: trellisEventVersion
    testImplementation group: 'org.trellisldp', name: 'trellis-constraint-rules', version: trellisConstraintVersion

    testRuntimeClasspath group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion

    buildToolsConfig 'org.trellisldp:trellis-build-tools:0.2.0'
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs.add("-Xlint:unchecked")
        options.compilerArgs.add("-Xlint:deprecation")
    }
}

test {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1

    if (org.gradle.internal.jvm.Jvm.current().getJavaVersion().isJava9Compatible()) {
        jvmArgs '--add-modules', 'java.xml.bind'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

release {
    tagTemplate = '$name-$version'
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', 'org.trellisldp.http'
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "org.trellisldp.http;version=${projectOsgiVersion}"
        instruction 'Require-Capability', '''\
            osgi.extender; filter:="(osgi.extender=osgi.serviceloader.processor)",
            osgi.serviceloader;
                filter:="(osgi.serviceloader=org.trellisldp.api.AuditService)";
                resolution:=optional; cardinality:=optional,
            osgi.serviceloader;
                filter:="(osgi.serviceloader=org.trellisldp.api.ConstraintService)";
                resolution:=optional; cardinality:=multiple
            '''
    }
}


task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar
    archives sourceJar
    archives (file('build/resources/main/features.xml')) {
        classifier 'features'
        type 'xml'
    }
}

jacoco {
    toolVersion = jacocoVersion
}

license {
    include "**/*.java"
    header rootProject.file('build/license/HEADER.txt')
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

processResources {
    outputs.upToDateWhen { false }
    expand project.properties
}

signing {
    required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        def sonatypeUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : ""
        def sonatypePassword = project.hasProperty('ossrhPassword') ? ossrhPassword : ""
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            packaging 'jar'
            url 'http://www.trellisldp.org'
            inceptionYear '2017'
            name 'Trellis Repository HTTP API'
            description 'The trellis repository HTTP API'

            organization {
                name project.vendor
                url project.homepage
            }

            developers {
                developer {
                    id 'acoburn'
                    name 'Aaron Coburn'
                    email 'acoburn @ (domain of organization url)'
                    organization 'Amherst College'
                    organizationUrl 'https://www.amherst.edu'
                    roles {
                        role 'developer'
                    }
                    timezone '-5'
                }
            }

            scm {
                connection 'scm:git:git://github.com/trellis-ldp/trellis-http.git'
                developerConnection 'scm:git:git@github.com/trellis-ldp/trellis-http.git'
                url 'https://github.com/trellis-ldp/trellis-http'
                tag 'HEAD'
            }

            licenses {
                license {
                    name 'Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0'
                    comments 'Copyright (c) 2017 Trellis LDP'
                }
            }
        }
    }
}

task processBuildTools(type: Copy) {
    from {
        configurations.buildToolsConfig.collect {
            zipTree(it).matching {
                include 'checkstyle/*.xml'
                include 'license/*.txt'
            }
        }
    }
    into 'build'
}

checkstyle {
    configFile = rootProject.file('build/checkstyle/checkstyle.xml')
    configProperties.checkstyleConfigDir = rootProject.file('build/checkstyle/')
    toolVersion = '8.1'
}

sonarqube {
    properties {
        property "sonar.projectName", "Trellis Repository HTTP API"
        property "sonar.projectKey", "org.trellisldp:trellis-http"
        property "sonar.links.homepage", "https://github.com/trellis-ldp/trellis"
        property "sonar.links.issue", "https://github.com/trellis-ldp/trellis-http/issues"
        property "sonar.links.scm_dev", "scm:git:git@github.com:trellis-ldp/trellis-http.git"
    }
}

task docs(type: Javadoc) {
    outputs.upToDateWhen { false }
    source sourceSets.main.allJava
    classpath = files(sourceSets.main.compileClasspath)
    destinationDir = new File(projectDir, "docs/${version}")
    options {
        links "http://docs.oracle.com/javase/8/docs/api/"
        links "http://docs.oracle.com/javaee/7/api/"
        links "https://commons.apache.org/proper/commons-rdf/apidocs/"
        links "https://trellis-ldp.github.io/trellis/apidocs/"
    }
}

checkstyleMain.dependsOn processBuildTools
afterReleaseBuild.dependsOn uploadArchives
afterReleaseBuild.dependsOn docs

